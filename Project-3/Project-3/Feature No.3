#include <Arduino.h>

void merge(float arr[], int left, int middle, int right)
{
  int i, j, k;
  int n1 = middle - left + 1;
  int n2 = right - middle;

  float LeftArray[n1], RightArray[n2];

  for (i = 0; i < n1; i++)
    LeftArray[i] = arr[left + i];
  for (j = 0; j < n2; j++)
    RightArray[j] = arr[middle + 1 + j];

  i = 0;
  j = 0;
  k = left;
  while (i < n1 && j < n2)
  {
    if (LeftArray[i] <= RightArray[j])
    {
      arr[k] = LeftArray[i];
      i++;
    }
    else
    {
      arr[k] = RightArray[j];
      j++;
    }
    k++;
  }

  while (i < n1)
  {
    arr[k] = LeftArray[i];
    i++;
    k++;
  }

  while (j < n2)
  {
    arr[k] = RightArray[j];
    j++;
    k++;
  }
}

void mergeSort(float arr[], int left, int right)
{
  if (left < right)
  {
    int middle = left + (right - left) / 2;

    mergeSort(arr, left, middle);
    mergeSort(arr, middle + 1, right);

    merge(arr, left, middle, right);
  }
}

void setup()
{
  Serial.begin(9600);
  Serial.println("Please enter the numbers you want in order (inside '[ ]' and separate with ','): ");
}

void loop()
{
  if (Serial.available())
  {
    String input = Serial.readStringUntil('\n');
    input.trim();

    int count = 1;
    for (size_t i = 0; i < input.length(); i++)
    {
      if (input.charAt(i) == ',')
        count++;
    }

    float numbers[count];
    int currentIndex = 0;
    String currentNumber = "";
    for (size_t i = 0; i < input.length(); i++)
    {
      char currentChar = input.charAt(i);
     if (currentChar == ',' || currentChar == '[' || currentChar == ']')
      {
        if (currentNumber != "")
        {
          numbers[currentIndex] = currentNumber.toFloat();
          currentIndex++;
          currentNumber = "";
        }
      }
      else if (currentChar != ' ')
      {
        currentNumber += currentChar;
      }
    }

    mergeSort(numbers, 0, count - 1);

    Serial.print("[");
    for (int i = 0; i < count; i++)
    {
      if (i != 0)
      {
        Serial.print(", ");
      }
      Serial.print(numbers[i]);
    }
    Serial.println("]");
  }
}
